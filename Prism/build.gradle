import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "xyz.jpenilla.run-paper" version "1.0.6"
}

apply plugin: 'java'

repositories {
    mavenLocal()
    mavenCentral()

    // Paper
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }

    // Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // WorldEdit
    maven { url = 'https://maven.enginehub.org/repo/' }

    // PasteGG
    maven { url = 'https://maven.addstar.com.au/artifactory/all-snapshot' }
}

group = "network.darkhelmet"
version = project.property("version")
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

processResources {
    filter ReplaceTokens, tokens: [
            "apiversion": project.property("apiversion"),
            "version"   : project.property("version")
    ]
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-core:7.2.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.0-SNAPSHOT'
    implementation 'io.papermc:paperlib:1.0.3'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'net.kyori:adventure-api:4.9.3'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.2-SNAPSHOT'
    implementation 'net.kyori:adventure-text-serializer-plain:4.9.3'
    implementation 'org.kitteh:paste-gg-api:1.0.0-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation project(':Prism-Api')
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

shadowJar {
    String fileName = "[1.16] Prism v" + project.version + ".jar"
    archiveFileName.set(fileName)
    dependencies {
        exclude(dependency('org.checkerframework:.*'))
        exclude(dependency('org.slf4j:.*'))
        exclude(dependency('org.intellij:.*'))
        exclude(dependency('org.jetbrains:.*'))
    }

    relocate 'io.papermc.lib', 'network.darkhelmet.prism.paperlib'
    relocate 'com.zaxxer', 'network.darkhelmet.prism.zaxxer'
    relocate 'net.kyori', 'network.darkhelmet.prism.kyori'
    relocate 'org.kitteh', 'network.darkhelmet.prism.kitteh'
    relocate 'org.bstats', 'io.github.rothes.prismcn.lib.org.bstats'

    minimize()
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.16.5")
}

gradle.buildFinished {
    deleteDirectory(new File(project.buildDir, "resources/"))
}

boolean deleteDirectory(File directoryToBeDeleted) {
    File[] allContents = directoryToBeDeleted.listFiles();
    if (allContents != null) {
        for (File file : allContents) {
            deleteDirectory(file);
        }
    }
    return directoryToBeDeleted.delete();
}